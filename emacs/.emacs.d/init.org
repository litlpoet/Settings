#+AUTHOR: Byungkuk Choi
#+email: litlpoet@gmail.com
#+STARTUP: fninline content
#+SEQ_TODO: TODO READY DONE

* init.el
** Preparation
*** load themes
#+BEGIN_SRC emacs-lisp
(use-package color-theme-sanityinc-tomorrow
  :ensure t
  :config (load-theme 'sanityinc-tomorrow-night t))
(use-package smart-mode-line
  :ensure t
  :init
  (setq sml/mode-width 'right)
  (setq sml/theme 'dark)
  (setq sml/name-width 35)
  (setq sml/extra-filler 7)
  (add-hook 'after-init-hook
            '(lambda()
               (sml/setup)
               (set-face-attribute `mode-line nil
                                   :family "Envy Code R"
                                   :box nil
                                   :height 1.0)
               (set-face-attribute `mode-line-inactive nil :box nil))))
#+END_SRC

*** set encoding
#+BEGIN_SRC emacs-lisp
(setq locale-coding-system 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(set-selection-coding-system 'utf-8)
(prefer-coding-system 'utf-8)
#+END_SRC

*** set fonts
#+BEGIN_SRC emacs-lisp
(if (eq system-type 'windows-nt)
    (add-to-list 'default-frame-alist '(font . "Source Code Pro-15"))
  (add-to-list 'default-frame-alist '(font . "DejaVu Sans Mono-14")))
(set-fontset-font t 'hangul (font-spec :name "NanumGothicCoding"))
#+END_SRC

*** set backup directory
#+BEGIN_SRC emacs-lisp
(defconst bk:backup-directory
  (expand-file-name "tmp" user-emacs-directory)
  "The temporary directory")
(unless (file-exists-p bk:backup-directory)
    (make-directory bk:backup-directory t))
(setq backup-directory-alist `(("." . ,bk:backup-directory)))
#+END_SRC


** Built-in Package Setup
*** dired
#+BEGIN_SRC emacs-lisp
(if (eq system-type 'windows-nt)
    (setq dired-listing-switches "-lha")
  (setq dired-listing-switches "-lha --group-directories-first"))
;; (add-hook 'dired-mode-hook 'auto-revert-mode)
#+END_SRC

*** prog-mode
#+BEGIN_SRC emacs-lisp
(add-hook 'prog-mode-hook #'rainbow-delimiters-mode)
;; (add-hook 'prog-mode-hook 'linum-mode)
(add-to-list 'auto-mode-alist '("\\.h\\'" . c++-mode))
#+END_SRC

*** auto-insert
#+BEGIN_SRC emacs-lisp
  (defun bk:format-include-guard-fallback ()
    "Generate an include guard string with file name."
    (let ((filename
           (file-name-nondirectory
            (file-name-sans-extension buffer-file-name)))
          (ext
           (file-name-extension buffer-file-name)))
      (replace-regexp-in-string
       "[^A-Z0-9_]" "_"
       (upcase (concat filename "_" ext "_")))))

  (defun bk:format-include-guard ()
    "Format an include guard, using projectile-project-root.
  If not in a projectile project use `bk:format-include-guard-fallback'."
    (if (projectile-project-p)
        (let ((filename
               (subseq
                (file-name-sans-extension buffer-file-name)
                (length (projectile-project-root))))
              (ext (file-name-extension buffer-file-name)))
          (replace-regexp-in-string
           "[^A-Z0-9_]" "_"
           (upcase (concat filename "_" ext "_"))))
      (bk:format-include-guard-fallback)))

  (defun bk:generate-include-guard ()
    "Generate an include guard (should be in a C/C++ file),
   used by `auto-insert-mode'."
    (insert "guard")
    (yas/expand))

  (defconst bk:auto-insert-dir
    (file-name-as-directory
     (expand-file-name "bk-inserts" user-emacs-directory)))

  (defvar bk:auto-insert-alist
    '(;; file pattern . ["filename-to-insert" insertion-function] or
      ;; (file pattern . description) . action (see `auto-insert-alist').
      (("\\.[hH]\\(h\\|pp\\)?$" . "C/C++ header") . bk:generate-include-guard)))

  (auto-insert-mode t)
  (setq auto-insert-directory bk:auto-insert-dir)
  (dolist (elem bk:auto-insert-alist)
    (add-to-list 'auto-insert-alist elem))
  (setq auto-insert-query nil)
#+END_SRC

*** auto-revert
#+BEGIN_SRC emacs-lisp
(use-package auto-revert
  :diminish auto-revert-mode
  :config
  (setq auto-revert-verbose nil)
  (global-auto-revert-mode))
#+END_SRC

*** whitespace
#+BEGIN_SRC emacs-lisp
(use-package whitespace
  :defer t
  :diminish whitespace-mode
  :init
  (add-hook 'diff-mode-hook
            '(lambda()
               (setq-local
                whitespace-style '(face
                                   tabs
                                   tab-mark
                                   trailing
                                   indentation::space
                                   indentation::tab
                                   spaces
                                   space-mark
                                   newline
                                   newline-mark))
               (whitespace-mode 1)))
  (add-hook 'prog-mode-hook
            '(lambda()
               (setq-local show-trailing-whitespace 1)
               (setq-local whitespace-style '(face
                                              tabs
                                              trailing
                                              lines-tail))
               (setq-local whitespace-line-column 80)
               (whitespace-mode 1))))
#+END_SRC


** External Package Setup
*** global core packages
**** org
#+BEGIN_SRC emacs-lisp
(use-package org
  :ensure t
  :bind (("C-c l" . org-store-link)
         ("C-c a" . org-agenda))
  :init
  (setq org-log-done t
        org-src-fontify-natively t
        org-src-window-setup 'current-window
        org-src-strip-leading-and-trailing-blank-lines t
        org-src-preserve-indentation t
        org-src-tab-acts-natively t)
  (add-hook 'org-mode-hook 'flyspell-mode))
#+END_SRC

**** projectile
#+BEGIN_SRC emacs-lisp
(use-package projectile
  :ensure t
  :init
  (setq projectile-indexing-method 'alien)
  (setq projectile-mode-line
        '(:eval (format " Prj[%s]" (projectile-project-name))))
  :config
  (projectile-global-mode))
#+END_SRC

**** helm
#+BEGIN_SRC emacs-lisp 
(use-package helm
  :ensure t
  :bind (("C-c h" . helm-command-prefix)
         ("C-x b" . helm-mini)
         ("C-x C-f" . helm-find-files)
         ("M-x" . helm-M-x)
         ("M-y" . helm-show-kill-ring))
  :commands (helm-execute-persistent-action
             helm-select-action)
  :diminish helm-mode
  :init
  (require 'helm-config)
  (helm-mode 1)
  :config
  (when (executable-find "curl")
    (setq helm-google-suggest-use-curl-p t))
  (setq helm-split-window-in-side-p t
        helm-move-to-line-cycle-in-source t
        helm-ff-search-library-in-sexp t
        helm-scroll-amount 8
        helm-ff-file-name-history-use-recentf t
        helm-M-x-fuzzy-match t
        helm-buffers-fuzzy-matching t
        helm-recentf-fuzzy-match t)
  (helm-autoresize-mode t)
  (unbind-key "C-x c")
  (bind-key "<tab>" 'helm-execute-persistent-action helm-map)
  (bind-key "C-i" 'helm-execute-persistent-action helm-map)
  (bind-key "C-z" 'helm-select-action helm-map)
  (use-package helm-ag
    :ensure t
    :config
    (setq helm-ag-insert-at-point 'symbol))
  (use-package helm-projectile
    :ensure t
    :config
    (helm-projectile-on)
    (setq projectile-completion-system 'helm)
    (setq projectile-switch-project-action 'projectile-dired)))
#+END_SRC

**** company
#+BEGIN_SRC emacs-lisp
(use-package company
  :ensure t
  :defer t
  :bind ("<tab>" . company-indent-or-complete-common)
  :diminish company-mode
  :init
  (add-hook 'after-init-hook 'global-company-mode)
  :config
  (setq company-selection-wrap-around t)
  (setq company-show-numbers t)
  (setq company-idle-delay nil)
  (use-package company-auctex
    :ensure t
    :config
    (company-auctex-init)))
#+END_SRC

**** flycheck
#+BEGIN_SRC emacs-lisp
(use-package flycheck
  :ensure t
  :defer t
  :init
  (add-hook 'after-init-hook #'global-flycheck-mode)
  (add-hook 'org-src-mode-hook
            '(lambda()
               (setq-local flycheck-disabled-checkers
                           '(emacs-lisp-checkdoc))))
  :config
  (use-package flycheck-irony
    :ensure t
    :init
    (add-hook 'flycheck-mode-hook #'flycheck-irony-setup))
  (use-package flycheck-google-cpplint
    :ensure t
    :config
    (flycheck-add-next-checker
     'irony
     '(warning . c/c++-googlelint))))
#+END_SRC

**** yasnippet
#+BEGIN_SRC emacs-lisp
(use-package yasnippet
  :ensure t
  :defer t
  :diminish yas-minor-mode
  :init
  (defconst bk:snippet-dir
    (file-name-as-directory
     (expand-file-name "bk-snippets" user-emacs-directory)))
  (if (file-exists-p bk:snippet-dir)
      (setq yas-snippet-dirs (list bk:snippet-dir)))
  :config
  (yas-global-mode 1))
#+END_SRC

**** recentf
#+BEGIN_SRC emacs-lisp
(use-package recentf
  :ensure t
  :commands recentf-mode
  :init
  (setq recentf-max-menu-items 10
        recentf=max-saved-items 1000)
  (add-hook 'after-init-hook 'recentf-mode))
#+END_SRC

**** undo-tree
#+BEGIN_SRC emacs-lisp
(use-package undo-tree
  :ensure t
  :diminish undo-tree-mode
  :config
  (global-undo-tree-mode))
#+END_SRC

**** avy
#+BEGIN_SRC emacs-lisp
(use-package avy
  :ensure t
  :bind ("C-c j" . avy-goto-word-or-subword-1))
#+END_SRC

**** ace-window
#+BEGIN_SRC emacs-lisp
(use-package ace-window
  :ensure t
  :bind ("C-x o" . ace-window))
#+END_SRC

*** global helper packages
**** smartparens
#+BEGIN_SRC emacs-lisp
(use-package smartparens
  :ensure t
  :defer t
  :diminish smartparens-mode
  :init
  (require 'smartparens-config)
  (smartparens-global-mode t)
  (show-smartparens-global-mode t))
#+END_SRC

**** clean-aindent-mode
#+BEGIN_SRC emacs-lisp
(use-package clean-aindent-mode
  :ensure t
  :defer t
  :init
  (add-hook 'prog-mode-hook 'clean-aindent-mode))
#+END_SRC

**** iedit
#+BEGIN_SRC emacs-lisp
(use-package iedit
  :ensure t
  :bind ("C-;" . iedit-mode)
  :config
  (set-face-inverse-video 'iedit-occurrence t))
#+END_SRC

**** expand-region
#+BEGIN_SRC emacs-lisp
(use-package expand-region
  :ensure t
  :bind ("M-2" . er/expand-region))
#+END_SRC

**** duplicate-thing
#+BEGIN_SRC emacs-lisp
(use-package duplicate-thing
  :ensure t
  :bind ("M-c" . duplicate-thing))
#+END_SRC

**** multiple-cursors
#+BEGIN_SRC emacs-lisp
(use-package multiple-cursors
  :ensure t
  :bind (("M-3" . mc/mark-next-like-this)
         ("M-4" . mc/mark-previous-like-this)
         ("M-#" . mc/unmark-next-like-this)
         ("M-$" . mc/unmark-previous-like-this)))
#+END_SRC

**** git-gutter-fringe
#+BEGIN_SRC emacs-lisp
(use-package git-gutter-fringe
  :ensure t
  :diminish git-gutter-mode
  :config
  (global-git-gutter-mode t))
#+END_SRC

**** volatile-highlights
#+BEGIN_SRC emacs-lisp
(use-package volatile-highlights
  :ensure t
  :diminish volatile-highlights-mode
  :config
  (set-face-inverse-video 'vhl/default-face t)
  (volatile-highlights-mode t))
#+END_SRC

*** mode-specific packages
**** paradox
#+BEGIN_SRC emacs-lisp
(use-package paradox
  :ensure t
  :defer t
  :init
  (setq paradox-github-token
        (with-temp-buffer
          (insert-file-contents
           (expand-file-name ".github_token" user-emacs-directory))
          (buffer-string)))
  (setq paradox-automatically-star t
        paradox-execute-asynchronously t
        paradox-display-download-count t
        paradox-column-width-package 24
        paradox-column-width-version 12))
#+END_SRC

**** irony
#+BEGIN_SRC emacs-lisp
(use-package irony
  :ensure t
  :defer t
  :init
  (defun bk:irony-mode-hook()
    (define-key irony-mode-map [remap completion-at-point]
      'irony-completion-at-point-async)
    (define-key irony-mode-map [remap complete-symbol]
      'irony-completion-at-point-async)
    (when (eq system-type 'windows-nt)
      (setq w32-pipe-read-delay 0)))
  (add-hook 'irony-mode-hook 'bk:irony-mode-hook)
  (add-hook 'irony-mode-hook 'irony-cdb-autosetup-compile-options)
  (add-hook 'c++-mode-hook 'irony-mode)
  (add-hook 'c-mode-hook
            '(lambda()
               (unless (derived-mode-p 'glsl-mode) (irony-mode)))))
#+END_SRC

**** company-irony
#+BEGIN_SRC emacs-lisp
(eval-after-load 'company
  '(progn
     (require 'company-irony)
     ;; (require 'company-irony-c-headers) ;; not mature yet
     (add-to-list 'company-backends 'company-irony)
     (add-hook 'irony-mode-hook 'company-irony-setup-begin-commands)))
#+END_SRC

**** company-c-headers
     this back-end should go before company-irony (using 'add-to-list')
#+BEGIN_SRC emacs-lisp
(eval-after-load 'company
  '(progn
     (require 'company-c-headers)
     (add-to-list 'company-backends 'company-c-headers)
     (when (eq system-type 'windows-nt)
       (setq
        company-c-headers-path-system
        '("c:/Local/msys64/mingw64/x86_64-w64-mingw32/include/"
          "c:/Local/msys64/mingw64/include/"
          "c:/Local/msys64/mingw64/include/c++/5.2.0/"
          "c:/Local/include/eigen3/")))
     (when (eq system-type 'gnu/linux)
       (setq
        company-c-headers-path-system
        (append
         company-c-headers-path-system
         '("/usr/include/c++/4.9/"
           "/usr/local/include/eigen3/"
           "/opt/qt5/5.5/gcc_64/include/"
           "/home/bk/VersionControl/Modules/libML"))))
     (setq company-c-headers-path-user '("." ".."))))
#+END_SRC

**** google-c-style
#+BEGIN_SRC emacs-lisp
(use-package google-c-style
  :ensure t
  :commands (google-set-c-style google-make-newline-indent)
  :init
  (add-hook 'c-mode-common-hook 'google-set-c-style)
  (add-hook 'c-mode-common-hook 'google-make-newline-indent))
#+END_SRC

**** clang-format
#+BEGIN_SRC emacs-lisp
(use-package clang-format
  :ensure t
  :commands clang-format-buffer
  :init
  (add-hook 'c-mode-hook 'bk:clang-format-setting)
  (add-hook 'c++-mode-hook 'bk:clang-format-setting)
  (defun bk:clang-format-setting()
    (bind-key "C-S-f" 'clang-format-buffer c-mode-base-map)
    (setq-local clang-format-style "Google")))
#+END_SRC

**** cmake-mode and cmake-font-lock
#+BEGIN_SRC emacs-lisp
(autoload 'cmake-font-lock-activate "cmake-font-lock" nil t)
(add-hook 'cmake-mode-hook 'cmake-font-lock-activate)
#+END_SRC

**** auctex
#+BEGIN_SRC emacs-lisp
(add-hook 'TeX-mode-hook 'linum-mode)
(add-hook 'TeX-mode-hook 'flyspell-mode)
(setq TeX-auto-save t)
(setq TeX-parse-self t)
(setq-default TeX-master nil)
(setq TeX-PDF-mode t)
(setq TeX-source-correlate-mode t)
(when (eq system-type 'windows-nt)
  (setq
   TeX-view-program-list
   '(("Sumatra PDF"
      ("\"C:/Program Files (x86)/SumatraPDF/SumatraPDF.exe\" -reuse-instance"
       (mode-io-correlate " -forward-search %b %n ")
       " %o"))))
  (eval-after-load 'tex
    '(progn
       (assq-delete-all 'output-pdf TeX-view-program-selection)
       (add-to-list 'TeX-view-program-selection
                    '(output-pdf "Sumatra PDF")))))
#+END_SRC

**** macrostep
#+BEGIN_SRC emacs-lisp
(use-package macrostep
  :ensure t
  :bind ("C-c e m" . macrostep-expand))
#+END_SRC



** Implementations
*** buffer specific display window
#+BEGIN_SRC emacs-lisp
(require 'rx)
(setq
 display-buffer-alist
 `(;; Put REPLs and error lists into the bottom side window
   (,(rx bos (or "*Flycheck errors*" ; Flycheck error list
                 "*compilation"      ; Compilation buffers
                 "*Warnings*"        ; Emacs warnings
                 "*shell"            ; Shell window
                 ))
    (display-buffer-reuse-window
     display-buffer-in-side-window)
    (side            . bottom)
    (reusable-frames . visible)
    (window-height   . 0.25))
   ("." nil (reusable-frames . visible))))
(defun bk:quit-bottom-side-windows ()
  "Quit windows at the bottom of the current frame."
  (interactive)
  (dolist (window (window-at-side-list nil 'bottom))
    (quit-window nil window)))
(bind-key "C-c q" 'bk:quit-bottom-side-windows)
#+END_SRC

*** window dedication
    setup a decicated window configuration
#+BEGIN_SRC emacs-lisp
(defun bk:toggle-current-window-dedication ()
  "Window dedication."
  (interactive)
  (let* ((window (selected-window))
         (dedicated (window-dedicated-p window)))
    (set-window-dedicated-p window (not dedicated))
    (message "Window %s dedicated to %s"
             (if dedicated "no longer " "")
             (buffer-name))))
(bind-key [pause] 'bk:toggle-current-window-dedication)
#+END_SRC

*** white space display in diff mode
    important white space in diff mode
#+BEGIN_SRC emacs-lisp

#+END_SRC

*** useful window title info.
#+BEGIN_SRC emacs-lisp
(setq
 frame-title-format
 '("" invocation-name ": "
   (:eval
    (if (buffer-file-name)
        (abbreviate-file-name (buffer-file-name))
      "%b"))))
#+END_SRC

*** hidden mode-line
#+BEGIN_SRC emacs-lisp
(defvar-local toggle-mode-line nil)
(defvar-local hide-mode-line nil)
(define-minor-mode toggle-mode-line
  "Minor mode to hide the mode-line in the current buffer."
  :init-value nil
  :global t
  :variable toggle-mode-line
  :group 'editing-basics
  (if toggle-mode-line
      (setq hide-mode-line mode-line-format
            mode-line-format nil)
    (setq mode-line-format hide-mode-line
          hide-mode-line nil))
  (force-mode-line-update)
  (redraw-display)
  (when (and (called-interactively-p 'interactive)
             toggle-mode-line)
    (run-with-idle-timer
     0 nil 'message
     (concat "Hidden mode line mode enabled.  "
             "Use M-x toggle-mode-line to make the mode-line appear."))))
;; If you want to hide the mode-line in every buffer by default
;; (add-hook 'after-change-major-mode-hook 'toggle-mode-line)
#+END_SRC


** Key Bindings
*** global keys
#+BEGIN_SRC emacs-lisp
(bind-key "M-9"    'backward-sexp)
(bind-key "M-0"    'forward-sexp)
(bind-key "M-1"    'delete-other-windows)
(bind-key "C-x k"  'kill-this-buffer)
(bind-key "RET"    'newline-and-indent)
(bind-key "<kana>" 'toggle-input-method)
#+END_SRC


** Alias
*** simplifying y-or-n prompt
#+BEGIN_SRC emacs-lisp
(defalias 'yes-or-no-p 'y-or-n-p)
#+END_SRC


** Platform Dependent Setup
*** windows
#+BEGIN_SRC emacs-lisp
(when (eq system-type 'windows-nt)
  (setenv "GIT_ASKPASS" "git-gui--askpass"))
#+END_SRC


* not used configuration
** preparation
*** control package archives
#+BEGIN_SRC emacs-lisp
(require 'package)
(add-to-list 'package-archives
             '("melpa" . "http://melpa.org/packages/") t)
(add-to-list 'package-archives
             '("org" . "http://orgmode.org/elpa/") t)
(package-initialize)
#+END_SRC

*** load custom file location
    Make customize setting separated from the init{.el, .org} files
    note: try to use customize for built-in packages
#+BEGIN_SRC emacs-lisp
(setq custom-file (expand-file-name "custom.el" user-emacs-directory))
(load custom-file)
#+END_SRC

*** load use-package
#+BEGIN_SRC emacs-lisp
;; Set-up use-package
;; use-package is used to configure the rest of the packages.
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))
(eval-when-compile (require 'use-package))
(require 'diminish)
(require 'bind-key)
(setq use-package-verbose t)
#+END_SRC


** check package installation

   1) Check if all packages are installed.
   2) If some packages are missing, install them automatically

#+BEGIN_SRC emacs-lisp 
(require 'cl)
(defun bk:packages-installed()
  (loop for p in package-selected-packages
        when (not (package-installed-p p))
        do (return nil)
        finally (return t)))

(unless (bk:packages-installed) ; check for new packages (package versions)
  (message "%s" "Emacs is now refreshing its package database...")
  (package-refresh-contents)
  (message "%s" "done.")
  (dolist (p package-selected-packages) ; install the missing packages
    (when (not (package-installed-p p))
      (package-install p))))
#+END_SRC

